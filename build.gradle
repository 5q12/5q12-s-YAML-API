plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

version = '1.1.2'
group = 'app.ccls.yml'

base {
    archivesName = 'yamlapi'
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java

            pom {
                name = 'YAML API'
                description = 'A simple API for reading and writing YAML files'
                url = 'https://github.com/5q12/5q12-s-YAML-API'
                
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = '5q12'
                        name = '5q12'
                        email = 'ccls.5q12@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/5q12/5q12-s-YAML-API.git'
                    developerConnection = 'scm:git:ssh://github.com/5q12/5q12-s-YAML-API.git'
                    url = 'https://github.com/5q12/5q12-s-YAML-API.git'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = uri("${rootProject.buildDir}/repo")
        }
    }
}
